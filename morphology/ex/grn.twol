Alphabet

A B Ã C D E Ẽ G G̃ H I Ĩ J K L M N Ñ O Õ P R S T U Ũ V W X Y Z Ỹ Á É Í Ó Ú Ý   F Q
a b ã c d e ẽ g g̃ h i ĩ j k l m n ñ o õ p r s t u ũ v w x y z ỹ á é í ó ú ý ʼ f q
%0 1 2 3 4 5 6 7 8 9

%{m%}:p
%{m%}:m
%{d%}:0
%{s%}:0
%{K%}:k
%{K%}:g
%{N%}:n
%{N%}:0
%-
%>:%>
;

Sets

Cns = B C D F G H J K L M N P Q R S T V W X Z G̃ Ñ 
      b c d f g h j k l m n p q r s t v w x z g̃ ñ ;

Nas = m n ñ ã ẽ ĩ õ ỹ ũ
      M N Ñ Ã Ẽ Ĩ Õ Ỹ Ũ ;

VowsTon = Á É Í Ó Ú Ý
          á é í ó ú ý ;

VowsAton = A E I O U Y 
           a e i o u y ;

ArchiCns = %{N%} %{K%} ;


Rules

"Change vowel to tonal before atonal suffix"

Va:Vt <=> _ %>: [%{m%}: e: | i: c: h: a:] ;
	  where Va in ( a e o u y )
                Vt in ( á é ó ú ý ) matched ;

"Delete ending -[i] before comparative -icha" 
Vx:0 <=> _ %>: i: c: h: a: ;
            where Vx in ( ĩ i í ) ;
 
"Change i to tonal before atonal suffixes"
i:í <=>  _ %>: %{m%}: e:;

"Remove morpheme boundary"
%>:0 <=> _ ;

"Remove word final hyphen"
%-:0 <=> _ .#. ;

"Surface [m] in loc affix after nasals"
%{m%}:m <=> Nas: %>: _ ;
	    %{d%}: %-: %>: _;
	    except
	    %{s%}: %>: _ ;

"Surface [n] in plural nouns" 
%{N%}:n <=> Nas: %>: _ ;

"Surface [g] in pl nouns after nasals" 
%{K%}:g <=> Nas: %>: %{N%}: _ ;

"Change tonal vowel to atonal if tonal in affix"
Vt:Va <=>  _ [ ? - VowsTon]+  VowsTon: ;
	  where Vt in ( á é ó ú ý ) Va in ( a e o u y ) matched;

"Always remove {d}"

%{d%}:0 <=> _ ;
